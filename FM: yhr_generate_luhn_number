
FUNCTION yhr_generate_luhn_number.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  EXPORTING
*"     REFERENCE(EV_NUMBER) TYPE  PRDNI
*"----------------------------------------------------------------------
*" FM: Generate ID number - 9 digits Based on Luhn algorithm
*"----------------------------------------------------------------------

  DATA:
    lv_num_n TYPE n LENGTH 8.

  TRY.

      DATA(lo_number) = cl_abap_random_int=>create( seed = cl_abap_random=>seed( ) " Random Seed Number
                                                    min  = 00000000                " Lower Limit for Value Area
                                                    max  = 99999999 ).             " Upper Limit for Value Area
    CATCH cx_abap_random.
      RETURN.
  ENDTRY.

  ev_number = lv_num_n = lo_number->get_next( ).  " get random 8-digit number

  DATA(lv_length) = strlen( ev_number ).
  DATA(lv_parity) = lv_length MOD 2.
  DATA(lv_index)  = 0.
  DATA(lv_sum)    = lv_index.

  DO lv_length TIMES.
    DATA(lv_digit) = CONV i( ev_number+lv_index(1) ).

    IF ( sy-index MOD 2 ) = lv_parity.

      lv_digit = lv_digit * 2.
      IF lv_digit > 9.
        lv_digit = lv_digit - 9.
      ENDIF.

    ENDIF.

    lv_sum = lv_sum + lv_digit.
    lv_index = lv_index + 1.
  ENDDO.

  lv_digit = ( 10 - ( lv_sum MOD 10 ) ) MOD 10.

* Add the last 9th digit
  DATA(lv_digit_str) = CONV string( lv_digit ).
  CONDENSE lv_digit_str NO-GAPS.
  ev_number = ev_number && lv_digit_str.

ENDFUNCTION.

